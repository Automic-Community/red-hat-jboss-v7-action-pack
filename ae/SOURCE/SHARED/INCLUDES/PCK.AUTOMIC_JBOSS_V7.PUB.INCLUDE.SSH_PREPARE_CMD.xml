<?xml version="1.0" encoding="ISO-8859-15" standalone="no"?><uc-export clientvers="11.0.0">
<JOBI name="PCK.AUTOMIC_JBOSS_V7.PUB.INCLUDE.SSH_PREPARE_CMD">
<HEADER state="1">
<Title/>
<ArchiveKey1/>
<ArchiveKey2/>
<OH_SubType/>
</HEADER>
<SCRIPT mode="1" state="1">
<MSCRI><![CDATA[!SSH Host
: SET &UC4RB_ESCAPED_STRING# =&UC4RB_SSH_HOST#
: INC PCK.ITPA_SHARED.PUB.INCLUDE.ESCAPE_ARG_VALUE
: SET &UC4RB_JAVA_CMD# = STR_CAT(&UC4RB_JAVA_CMD#, " -sshhost &UC4RB_ESCAPED_STRING#")

!SSH Port
: SET &UC4RB_JAVA_CMD# = STR_CAT(&UC4RB_JAVA_CMD#, " -sshport &UC4RB_SSH_PORT#")

!Connection Timeout
: SET &UC4RB_JAVA_CMD# = STR_CAT(&UC4RB_JAVA_CMD#, " -conntimeout &UC4RB_SSH_CONN_TIMEOUT#")

!Command Timeout
: SET &UC4RB_JAVA_CMD# = STR_CAT(&UC4RB_JAVA_CMD#, " -sshtimeout &UC4RB_SSH_TIMEOUT#")

: IF &UC4RB_SUDO_SSH_PASSWORD# NE "" OR "--10"
:   SET &UC4RB_ENCRYPTED_PASSWORD_TMP# = "&UC4RB_SUDO_SSH_PASSWORD#"
!   Decrypt passphrase
:   INC PCK.ITPA_SHARED.PRV.INCLUDE.DECRYPT_PASSWORD
:   IF &UC4RB_AGENT_OS# = "WINDOWS"
:     IF &UC_WIN_TYP = "EXTCOMPROC"
        $env:UC4_DECRYPTED_SUDO_PASS=$UC4_DECRYPTED_PWD
:     ELSE
        set UC4_DECRYPTED_SUDO_PASS=%UC4_DECRYPTED_PWD%
:     ENDIF
:   ELSE
      UC4_DECRYPTED_SUDO_PASS=$UC4_DECRYPTED_PWD
      export UC4_DECRYPTED_SUDO_PASS
:   ENDIF
: ENDIF

!SSH Username
: SET &UC4RB_ESCAPED_STRING# =&UC4RB_SSH_USERNAME#
: INC PCK.ITPA_SHARED.PUB.INCLUDE.ESCAPE_ARG_VALUE
: SET &UC4RB_JAVA_CMD# = STR_CAT(&UC4RB_JAVA_CMD#, " -username &UC4RB_ESCAPED_STRING#")


: IF &UC4RB_SSH_KEY# NE ""
:   SET &UC4RB_ESCAPED_STRING# =&UC4RB_SSH_KEY#
:   INC PCK.ITPA_SHARED.PUB.INCLUDE.ESCAPE_ARG_VALUE
:   SET &UC4RB_JAVA_CMD# = STR_CAT(&UC4RB_JAVA_CMD#, " -sshkey &UC4RB_ESCAPED_STRING#")

!SSH Options
: IF &UC4RB_SSH_OPTIONS# <> ""
!  Adding Strict host Key Check in command
:  SET &POS# = STR_FIND(&UC4RB_SSH_OPTIONS#,"Strict Host Key Check")
:  IF &POS# <> 0
:    SET &UC4RB_JAVA_CMD# = STR_CAT(&UC4RB_JAVA_CMD#, " -hostkeycheck YES")
:  ENDIF
!  Adding Allocate TTY in command
:  SET &POS# = STR_FIND(&UC4RB_SSH_OPTIONS#,"Allocate TT")
:  IF &POS# <> 0
:    SET &UC4RB_JAVA_CMD# = STR_CAT(&UC4RB_JAVA_CMD#, " -tty YES")
:  ENDIF
!  Adding Sudo in command
:  SET &POS# = STR_FIND(&UC4RB_SSH_OPTIONS#,"Sudo")
:  IF &POS# <> 0
:    SET &UC4RB_JAVA_CMD# = STR_CAT(&UC4RB_JAVA_CMD#, " -sudo YES")
:  ENDIF
: ENDIF

:   IF &UC4RB_SSH_PASSPHRASE# NE "" OR "--10"
:     SET &UC4RB_ENCRYPTED_PASSWORD_TMP# = "&UC4RB_SSH_PASSPHRASE#"
!     Decrypt passphrase
:    INC PCK.ITPA_SHARED.PRV.INCLUDE.DECRYPT_PASSWORD
:    IF &UC4RB_AGENT_OS# = "WINDOWS"
:      IF &UC_WIN_TYP = "EXTCOMPROC"
        $env:UC4_DECRYPTED_PASSPHRASE=$UC4_DECRYPTED_PWD
:      ELSE
         set UC4_DECRYPTED_PASSPHRASE=%UC4_DECRYPTED_PWD%
:      ENDIF
:    ELSE
       UC4_DECRYPTED_PASSPHRASE=$UC4_DECRYPTED_PWD
       export UC4_DECRYPTED_PASSPHRASE
:    ENDIF
:   ENDIF
: ELSE
!  Decrypt Password
:  SET &UC4RB_ENCRYPTED_PASSWORD_TMP# = "&UC4RB_SSH_PASSWORD#"
:  INC PCK.ITPA_SHARED.PRV.INCLUDE.DECRYPT_PASSWORD
:  IF &UC4RB_AGENT_OS# = "WINDOWS"
:    IF &UC_WIN_TYP = "EXTCOMPROC"
       $env:UC4_DECRYPTED_PWD=$UC4_DECRYPTED_PWD
:    ELSE
       set UC4_DECRYPTED_PWD=%UC4_DECRYPTED_PWD%
:    ENDIF
:  ELSE
     export UC4_DECRYPTED_PWD
:  ENDIF
: ENDIF

: SET &UC4RB_ESCAPED_STRING# =&UC4RB_CMD_FILE_PATH#
: INC PCK.ITPA_SHARED.PUB.INCLUDE.ESCAPE_ARG_VALUE
: SET &UC4RB_JAVA_CMD# = STR_CAT(&UC4RB_JAVA_CMD#, " -cmdfile &UC4RB_ESCAPED_STRING#")

: SET &UC4RB_JAVA_CMD# = STR_CAT(&UC4RB_JAVA_CMD#, " -outputmode &UC4RB_OUTPUTMODE#")

: IF &UC4RB_OUTPUTMODE# NE "0"
:   SET &AGENT_TEMPDIR# = GET_VAR('UC_EX_PATH_TEMP', &AGENT#)
:   SET &AGENT_TEMPDIR# = "&AGENT_TEMPDIR#tempdir-&$RUNID#"

!   create std output log file
:   SET &UC4RB_FILE_PATH# = STR_CAT(&AGENT_TEMPDIR#,"/output-&$RUNID#.log")
:   SET &UC4RB_ESCAPED_STRING# =&UC4RB_FILE_PATH#
:   INC PCK.ITPA_SHARED.PUB.INCLUDE.ESCAPE_ARG_VALUE
:   SET &UC4RB_JAVA_CMD# = STR_CAT(&UC4RB_JAVA_CMD#, " -outfile &UC4RB_ESCAPED_STRING#")
:   SET &UC4RB_OUTPUT_LOG# = &UC4RB_FILE_PATH#

!   create std error log file
:   SET &UC4RB_FILE_PATH# = STR_CAT(&AGENT_TEMPDIR#,"/error-&$RUNID#.log")
:   SET &UC4RB_ESCAPED_STRING# =&UC4RB_FILE_PATH#
:   INC PCK.ITPA_SHARED.PUB.INCLUDE.ESCAPE_ARG_VALUE
:   SET &UC4RB_JAVA_CMD# = STR_CAT(&UC4RB_JAVA_CMD#, " -errfile &UC4RB_ESCAPED_STRING#")
: ENDIF
]]></MSCRI>
</SCRIPT>
<DOCU_Docu state="1" type="text">
<DOC/>
</DOCU_Docu>
</JOBI>
</uc-export>